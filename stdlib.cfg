allowcommand echo -1 // -1 means all params
allowcommand me -1
allowcommand whisper -1
allowcommand givemaster 1 1 // 1 param, master only
allowcommand sendto 1 1 // send map to cn, master only
allowcommand stats
allowcommand login 1

alias spec [ if (getalias arg1) [ spectator 1 $arg1 ] ]
alias unspec [ if (getalias arg1) [ spectator 0 $arg1 ] ]
alias specall [ looplist a (listclients) [ spectator 1 $a ] ]
alias unspecall [ looplist a (listclients) [ spectator 0 $a ] ]

alias whoismaster [
	looplist c (listclients) [
		if (isadmin $c) [
			echo admin is (concatword (getclientname $c) "(" $c ")" ) (getclientip $c)/(getclienthostname $c) (concatword "(" (getclientcountry $c) ")" )
		];
		if (ismaster $c) [
			echo master is (concatword (getclientname $c) "(" $c ") (" (getclientcountry $c) " " (getclientip $c) " " (getclienthostname $c) ")" )
		];
	];
];
allowcommand whoismaster
alias blacklistmaster [
	looplist c (listclients) [
		if (ismaster $c) [
			echo "Blacklisting master " (getclientname $c);
			blacklist (getclientip $c) (getalias arg1);
			takemaster;
			clearbans;
		]
	]
]
alias kickmaster [
	looplist c (listclients) [
		if (ismaster $c) [
			echo "Kicking master " (getclientname $c);
			kick (getclientip $c);
		]
	]
]

alias who [
	s = "";
	if (=s (listclients) "") [
		echo "No clients on the server."
	] [
	looplist c (listclients) [
		if (> (strlen $s) 400) [
			echo $s;
			s = ""
		]
		if (ismaster $c) [
			s = (concatword $s "^f0");
		] [
			if (isadmin $c) [
				s = (concatword $s "^f6");
			] [
				s = (concatword $s "^f7");
			]
		]
		s = (concatword $s (getclientname $c) " ^f2(^f5" $c " ^f7" (getclientcountry $c) "^f2 ^f5" (getclientip $c) "^f2 ^f7" (getclienthostname $c) "^f2)  ")
	];
	echo $s;
	]
]
allowcommand who
